buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}
dependencies {
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.github.gabrielemariotti.cards:cardslib-core:2.0.1'
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.nhaarman.listviewanimations:library:2.6.0'
    compile 'org.apache.commons:commons-lang3:+'
    compile 'commons-io:commons-io:2.4+'
    compile files('@CMAKE_BINARY_DIR@/thirdparty/android/jna/jna.jar')
    compile files('libs/RootTools-3.4.jar')
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    packagingOptions {
        // Both commons-lang3 and common-io want to include these file
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        disable 'ExtraTranslation'
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "com.github.chenxiaolong.dualbootpatcher"
        minSdkVersion 17
        targetSdkVersion 21
        versionCode 1
        versionName "@MBP_VERSION@"
    }

    signingConfigs {
        release {
            storeFile file("/dev/null")
            keyAlias ""
            storePassword ""
            keyPassword ""
        }

        debug
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_release"
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_debug"
        }

        ci {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".snapshot"
            versionNameSuffix "-SNAPSHOT"
            buildConfigField "int", "APP_NAME_RESOURCE", "R.string.app_name_snapshot"
        }
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleRelease)) {
            def propSigning = new File('signing.properties')

            if (propSigning.canRead()) {
                def Properties props = new Properties()
                props.load(new FileInputStream(propSigning))

                if (props != null && props.containsKey('storeFile')) {
                    signingConfigs.release.storeFile = file(props['storeFile'])
                    signingConfigs.release.storePassword = new String(System.console().readPassword("Enter keystore password: "))
                    signingConfigs.release.keyAlias = System.console().readLine("Enter alias key: ")
                    signingConfigs.release.keyPassword = new String(System.console().readPassword("Enter key password: "))
                }
                else {
                    buildTypes.release.signingConfig = null
                }
            }
            else {
                buildTypes.release.signingConfig = null
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = [
                '@CMAKE_BINARY_DIR@/assets/',
                'assets'
            ]
            jniLibs.srcDirs = [
                '@CMAKE_BINARY_DIR@/libmbp/out/',
                '@CMAKE_BINARY_DIR@/libmiscstuff/out/',
                '@CMAKE_BINARY_DIR@/thirdparty/android/jna/lib/'
            ]
        }

        debug {
            manifest.srcFile 'manifests/debug.xml'
        }

        ci {
            manifest.srcFile 'manifests/ci.xml'
        }
    }
}
